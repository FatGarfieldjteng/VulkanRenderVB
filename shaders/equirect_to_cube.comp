#version 450

layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform sampler2D equirectMap;
layout(set = 0, binding = 1, rgba16f) uniform writeonly image2DArray cubeMap;

const float PI     = 3.14159265359;
const float TWO_PI = 6.28318530718;

vec3 getCubeDir(ivec3 coord, float size) {
    vec2 uv = (vec2(coord.xy) + 0.5) / size * 2.0 - 1.0;
    switch (coord.z) {
        case 0: return normalize(vec3( 1.0, -uv.y, -uv.x));
        case 1: return normalize(vec3(-1.0, -uv.y,  uv.x));
        case 2: return normalize(vec3( uv.x,  1.0,  uv.y));
        case 3: return normalize(vec3( uv.x, -1.0, -uv.y));
        case 4: return normalize(vec3( uv.x, -uv.y,  1.0));
        case 5: return normalize(vec3(-uv.x, -uv.y, -1.0));
    }
    return vec3(0.0);
}

void main() {
    ivec3 coord = ivec3(gl_GlobalInvocationID);
    int size = imageSize(cubeMap).x;
    if (coord.x >= size || coord.y >= size || coord.z >= 6) return;

    vec3 dir = getCubeDir(coord, float(size));

    float phi   = atan(dir.z, dir.x);
    float theta = asin(clamp(dir.y, -1.0, 1.0));
    vec2 equiUV = vec2(phi / TWO_PI + 0.5, 0.5 - theta / PI);

    vec4 color = textureLod(equirectMap, equiUV, 0.0);
    imageStore(cubeMap, coord, color);
}
